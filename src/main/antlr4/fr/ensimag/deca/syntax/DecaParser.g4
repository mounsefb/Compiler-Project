parser grammar DecaParser;

options {
    // Default language but name it anyway
    //
    language  = Java;

    // Use a superclass to implement all helper
    // methods, instance variables and overrides
    // of ANTLR default methods, such as error
    // handling.
    //
    superClass = AbstractDecaParser;

    // Use the vocabulary generated by the accompanying
    // lexer. Maven knows how to work out the relationship
    // between the lexer and parser and will build the
    // lexer before the parser. It will also rebuild the
    // parser if the lexer changes.
    //
    tokenVocab = DecaLexer;

}

// which packages should be imported?
@header {
    import fr.ensimag.deca.tree.*;
    import fr.ensimag.deca.tools.*;
    import java.io.PrintStream;
}

@members {
    @Override
    protected AbstractProgram parseProgram() {
        return prog().tree;
    }
}

prog returns[AbstractProgram tree]
    : list_classes main EOF {
            assert($list_classes.tree != null);
            assert($main.tree != null);
            $tree = new Program($list_classes.tree, $main.tree);
            setLocation($tree, $list_classes.start);
        }
    ;


main returns[AbstractMain tree]
    : /* epsilon */ {
            $tree = new EmptyMain();
        }
    | block {
            assert($block.decls != null);
            assert($block.insts != null);
            $tree = new Main($block.decls, $block.insts);
            setLocation($tree, $block.start);
        }
    ;

block returns[ListDeclVar decls, ListInst insts]
    : OBRACE list_decl list_inst CBRACE {
            assert($list_decl.tree != null);
            assert($list_inst.tree != null);
            $decls = $list_decl.tree;
            $insts = $list_inst.tree;
        }
    ;

list_decl returns[ListDeclVar tree]
@init   {
            $tree = new ListDeclVar();
        }
    : decl_var_set[$tree]*
    ;

decl_var_set[ListDeclVar l]
    : type list_decl_var[$l,$type.tree] SEMI
    ;

list_decl_var[ListDeclVar l, AbstractIdentifier t]
    : dv1=decl_var[$t] {
        assert($dv1.tree != null);
        $l.add($dv1.tree);
        } (COMMA dv2=decl_var[$t] {
            assert($dv2.tree != null);
            $l.add($dv2.tree);
        }
      )*
    ;


decl_var[AbstractIdentifier t] returns[AbstractDeclVar tree]
@init   {
    DeclVar tree;
    NoInitialization noinitialization;
    Initialization initialization;
        }
    : i=ident {
        assert($i.tree != null);
        noinitialization=new NoInitialization();
        $tree = new DeclVar($t,  $i.tree, noinitialization);
        setLocation($tree,$i.start);
        }

      (EQUALS e=expr {
        assert($e.tree != null );
        initialization=new Initialization($e.tree);
        $tree = new DeclVar($t,  $i.tree, initialization);
        setLocation(initialization,$e.start);
        setLocation($tree,$EQUALS);
        }
      )? {
        }
    ;

list_inst returns[ListInst tree]
@init {
    $tree = new ListInst();
    }
    : (e=inst {
        assert($e.tree != null);
        $tree.add($e.tree);
        }
      )*
    ;

inst returns[AbstractInst tree]
    : e1=expr SEMI {
            assert($e1.tree != null);
            $tree = $e1.tree;
            setLocation($tree, $e1.start);
        }
    | SEMI {
            $tree = new NoOperation();
            setLocation($tree, $SEMI);
        }
    | PRINT OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(false, $list_expr.tree) ;
            setLocation($tree, $PRINT);
        }
    | PRINTLN OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(false, $list_expr.tree) ;
            setLocation($tree, $PRINTLN);
        }
    | PRINTX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Print(true, $list_expr.tree) ;
            setLocation($tree, $PRINTX);

        }
    | PRINTLNX OPARENT list_expr CPARENT SEMI {
            assert($list_expr.tree != null);
            $tree = new Println(true, $list_expr.tree) ;
            setLocation($tree, $PRINTLNX);

        }
    | if_then_else {
            assert($if_then_else.tree != null);
            $tree=$if_then_else.tree;
            setLocation($tree,$if_then_else.start);
            
        }
    | WHILE OPARENT condition=expr CPARENT OBRACE body=list_inst CBRACE {
            assert($condition.tree != null);
            assert($body.tree != null);
            $tree = new While($condition.tree , $body.tree);
            setLocation($tree,$WHILE);
        }
    | RETURN expr SEMI {
            assert($expr.tree != null);
            $tree = new Return($expr.tree);
            setLocation($tree, $RETURN);
        }
    ;

if_then_else returns[IfThenElse tree]
@init {
        ListInst elseifBranch = new ListInst();
        ListInst elseBranch = new ListInst();
        IfThenElse elseiftree; 
    }
    : if1=IF OPARENT condition=expr CPARENT OBRACE li_if=list_inst CBRACE {
        $tree = new IfThenElse($condition.tree, $li_if.tree, elseBranch);
        setLocation($tree, $if1);
        }
      (ELSE elsif=IF OPARENT elsif_cond=expr CPARENT OBRACE elsif_li=list_inst CBRACE {
        elseifBranch = new ListInst();
        elseiftree = new IfThenElse($elsif_cond.tree, $elsif_li.tree, elseifBranch);
        elseBranch.add(elseiftree);
        elseBranch = elseifBranch;
        setLocation(elseiftree, $ELSE);
        }   
      )*
      (ELSE OBRACE li_else=list_inst CBRACE {
            assert($li_else.tree != null);
            for (AbstractInst inst : $li_else.tree.getList()){
                    elseBranch.add(inst); 
            }
        }
      )?
    ;

list_expr returns[ListExpr tree]
@init   {
        $tree = new ListExpr();
        }
    : (e1=expr {
        assert($e1.tree != null);
        $tree.add($e1.tree);
        }
       (COMMA e2=expr {
        assert($e2.tree != null);
        $tree.add($e2.tree);
        }
       )* )?
    ;

expr returns[AbstractExpr tree]
    : e1=assign_expr {
            assert($e1.tree != null);
            $tree = $e1.tree;
            setLocation($tree, $e1.start);
        }
    ;

assign_expr returns[AbstractExpr tree]
    : e=or_expr (
        /* condition: expression e must be a "LVALUE" */ {
            if (! ($e.tree instanceof AbstractLValue)) {
                throw new InvalidLValue(this, $ctx);
            }
        }
        EQUALS e2=assign_expr {
            assert($e.tree != null);
            assert($e2.tree != null);
            $tree = new Assign((AbstractLValue)$e.tree, $e2.tree);
            setLocation($tree, $EQUALS);
        }
      | /* epsilon */ {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
      )
    ;

or_expr returns[AbstractExpr tree]
    : e=and_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=or_expr OR e2=and_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Or($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);

       }
    ;

and_expr returns[AbstractExpr tree]
    : e=eq_neq_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    |  e1=and_expr AND e2=eq_neq_expr {
            assert($e1.tree != null);                         
            assert($e2.tree != null);
            $tree = new And($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

eq_neq_expr returns[AbstractExpr tree]
    : e=inequality_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=eq_neq_expr EQEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Equals($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=eq_neq_expr NEQ e2=inequality_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new NotEquals($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

inequality_expr returns[AbstractExpr tree]
    : e=sum_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=inequality_expr LEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new LowerOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
 
        }
    | e1=inequality_expr GEQ e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new GreaterOrEqual($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);

        }
    | e1=inequality_expr GT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Greater($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr LT e2=sum_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Lower($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=inequality_expr INSTANCEOF type {
            assert($e1.tree != null);
            assert($type.tree != null);
            $tree = new InstanceOf($e1.tree, $type.tree);
            setLocation($tree, $e1.start);
        }
    ;


sum_expr returns[AbstractExpr tree]
    : e=mult_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=sum_expr PLUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Plus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=sum_expr MINUS e2=mult_expr {
            assert($e1.tree != null);
            assert($e2.tree != null);
            $tree = new Minus($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

mult_expr returns[AbstractExpr tree]
    : e=unary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=mult_expr TIMES e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Multiply($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);

        }
    | e1=mult_expr SLASH e2=unary_expr {
            assert($e1.tree != null);                                         
            assert($e2.tree != null);
            $tree = new Divide($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    | e1=mult_expr PERCENT e2=unary_expr {
            assert($e1.tree != null);                                                                          
            assert($e2.tree != null);
            $tree = new Modulo($e1.tree, $e2.tree);
            setLocation($tree, $e1.start);
        }
    ;

unary_expr returns[AbstractExpr tree]
    : op=MINUS e=unary_expr {
            assert($e.tree != null);
            $tree = new UnaryMinus($e.tree);
            setLocation($tree, $e.start);
        }
    | op=EXCLAM e=unary_expr {
            assert($e.tree != null);
            $tree = new Not($e.tree);
            setLocation($tree, $e.start);
        }
    | s=select_expr {
            assert($s.tree != null);
            $tree = $s.tree;
            setLocation($tree, $s.start);
        }
    ;

select_expr returns[AbstractExpr tree]
    : e=primary_expr {
            assert($e.tree != null);
            $tree = $e.tree;
            setLocation($tree, $e.start);
        }
    | e1=select_expr DOT i=ident {
            assert($e1.tree != null);
            assert($i.tree != null);
        }
        (o=OPARENT args=list_expr CPARENT {
            // we matched "e1.i(args)"
            assert($args.tree != null);
            $tree = new MethodCall($e1.tree, $i.tree, $args.tree);
            setLocation($tree, $o);
        }
        | /* epsilon */ {
            // we matched "e.i"
            $tree = new Selection($e1.tree, $i.tree);
             setLocation($tree, $e1.start);
        }
        )
    ;

primary_expr returns[AbstractExpr tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree,$ident.start);

        }
    | m=ident OPARENT args=list_expr CPARENT {
            assert($args.tree != null);
            assert($m.tree != null);
            ThisLiteral thisLit = new ThisLiteral(true);
            setLocation(thisLit, $m.start);
            assert(thisLit != null);
            $tree = new MethodCall(thisLit, $m.tree, $args.tree);
            setLocation($tree, $m.start);
        }
    | OPARENT expr CPARENT {
            assert($expr.tree != null);
            $tree = $expr.tree;
            setLocation($tree, $expr.start);
        }
    | READINT OPARENT CPARENT {
        try{
            $tree=new ReadInt();
            assert($tree != null);
            setLocation($tree, $READINT);
        } catch (Exception e) {
            $tree = null;
        }
        }
    | READFLOAT OPARENT CPARENT {
        try{
            $tree=new ReadFloat();
            assert($tree != null);
            setLocation($tree, $READFLOAT);
        } catch (Exception e) {
            $tree = null;
        }

        }
    | NEW ident OPARENT CPARENT {
            assert($ident.tree != null);
            $tree = new New($ident.tree);
            setLocation($tree, $NEW);
        }
    | cast=OPARENT type CPARENT OPARENT expr CPARENT {
            assert($type.tree != null);
            assert($expr.tree != null);
            $tree = new Cast($type.tree, $expr.tree);
            setLocation($tree, $type.start);
        }
    | literal {
            assert($literal.tree != null);
            $tree = $literal.tree ;
            setLocation($tree,$literal.start);
        }
    ;

type returns[AbstractIdentifier tree]
    : ident {
            assert($ident.tree != null);
            $tree = $ident.tree;
            setLocation($tree,$ident.start);
        }
    ;

literal returns[AbstractExpr tree]
    : i=INT {
        try{
            $tree=new IntLiteral(Integer.parseInt($i.text));
            setLocation($tree, $i);
        } catch (NumberFormatException e) {
           IntegerTooLargeException err = new IntegerTooLargeException(this, $i);
           throw err;
        }
        }
    | fd=FLOAT {
        try{ 
            String value = $fd.text;
            Float fvalue = Float.parseFloat(value);
            if(fvalue == 0.0){
                for (char c : value.toCharArray()) {
                    if (c == '1' || c == '2' || c == '3' || c == '4' || c == '5' || c == '6' || c == '7' || c == '8' || c == '9' ) {
                        throw new FloatTooSmallException(this,$fd);
                    }
                }
            }
            $tree=new FloatLiteral(fvalue);
            setLocation($tree, $fd);
        } catch (IllegalArgumentException e) {
            FloatTooLargeException err = new FloatTooLargeException(this, $fd);
            throw err;
        
        }
        }
    | STRING {
        try{
            $tree=new StringLiteral($STRING.text.substring(1, $STRING.text.length() - 1));
            setLocation($tree, $STRING);
        } catch (Exception e) {
            $tree = null;
        }
        }
    | TRUE {
        try{
            $tree = new BooleanLiteral(true);
            setLocation($tree, $TRUE);
        } catch (Exception e) {
            $tree = null;
        }
        }
    | FALSE {
        try{
            $tree = new BooleanLiteral(false);
            setLocation($tree, $FALSE);
        } catch (Exception e) {
            $tree = null;
        }
        }
    | THIS {
        try{
            $tree = new ThisLiteral(false);
            setLocation($tree, $THIS);
        } catch (Exception e) {
            $tree = null;
        }
        }
    | NULL {
        try{
            $tree = new NullLiteral();
            setLocation($tree, $NULL);
        } catch (Exception e) {
            $tree = null;
        }
        }
    ;

ident returns[AbstractIdentifier tree]
    : IDENT {
        try{
            $tree = new Identifier(this.getDecacCompiler().createSymbol($IDENT.text));
            setLocation($tree, $IDENT);
        } catch (Exception e) {
            $tree = null;
        }
        }
    ;

/****     Class related rules     ****/

list_classes returns[ListDeclClass tree]
@init   {
        $tree = new ListDeclClass();
        }
    :
      (c1=class_decl {
        assert($c1.tree != null);
        $tree.add($c1.tree);
        setLocation($tree, $c1.start);
        }
      )*
    ;
class_decl returns[AbstractDeclClass tree]
    : CLASS name=ident superclass=class_extension OBRACE class_body CBRACE {
        assert($name.tree != null);
        assert($class_body.listField != null);
        assert($class_body.listMeth != null);
        $tree = new DeclClass($name.tree, $superclass.tree, $class_body.listField, $class_body.listMeth);
        setLocation($tree, $CLASS);
        setLocation($superclass.tree,$superclass.start);
        }
    ;

class_extension returns[AbstractIdentifier tree]
    : EXTENDS ident {
        assert($ident.tree != null);
        $tree = $ident.tree;
        setLocation($tree, $EXTENDS);
        }
    | /* epsilon */ {
        $tree = new Identifier(this.getDecacCompiler().createSymbol("Object"));
        }
    ;

class_body returns[ListDeclField listField, ListDeclMethod listMeth]
@init  {
        $listMeth = new ListDeclMethod();
        $listField = new ListDeclField();
    }
    : (m=decl_method {
        assert($m.tree != null);
        $listMeth.add($m.tree);
        setLocation($listMeth, $m.start);
        }
      | decl_field_set[$listField]
      )*
    ;

decl_field_set[ListDeclField l] 
    : v=visibility t=type list_decl_field[$l, $t.tree, $v.tree]
      SEMI
    ;

visibility returns[Visibility tree]
    : /* epsilon */ {
        $tree = Visibility.PUBLIC;
        }
    | PROTECTED {
        $tree = Visibility.PROTECTED;
    }
    ;

list_decl_field[ListDeclField l, AbstractIdentifier t, Visibility v]
    : dv1=decl_field[$t, $v]{
        assert($dv1.tree != null);
        $l.add($dv1.tree);
    }
        (COMMA dv2=decl_field[$t, $v] {
            assert($dv2.tree != null);
            $l.add($dv2.tree);
        }
      )*
    ;

decl_field[AbstractIdentifier t, Visibility v] returns[AbstractDeclField tree]
@init   {
    AbstractDeclField tree;
    AbstractInitialization initialization;
}
    : i=ident {
        assert($i.tree != null);
        initialization=new NoInitialization();
        assert(initialization != null);
        $tree = new DeclField($t, $i.tree, initialization, $v);
        setLocation($tree,$i.start);
        }
      (EQUALS e=expr {
        assert($e.tree != null );
        initialization=new Initialization($e.tree);
        $tree = new DeclField($t, $i.tree, initialization, $v);
        setLocation($tree, $EQUALS);
        setLocation(initialization,$e.start);
        }
      )? {
        }
    ;



    

decl_method returns[AbstractDeclMethod tree]
@init {
    DeclMethod tree;
    AbstractMethodBody methBody;
}
    : type ident OPARENT params=list_params CPARENT (block {
        methBody = new MethodBody($block.decls, $block.insts);
        setLocation(methBody, $block.start);
        }
      | ASM OPARENT code=multi_line_string CPARENT SEMI {
        methBody = new MethodAsmBody(new StringLiteral($code.text));
        setLocation(methBody, $ASM);
        }
      ) {
        assert($ident.tree != null);
        $tree = new DeclMethod($type.tree, $ident.tree, $params.tree, methBody);
        setLocation($tree,$type.start);
        }
    ;

list_params returns[ListDeclParam tree]
@init   {
    $tree = new ListDeclParam();
}
    : (p1=param { 
        assert($p1.tree != null);
        $tree.add($p1.tree);
        setLocation($tree, $p1.start);
        } (COMMA p2=param {
        assert($p2.tree != null);
        $tree.add($p2.tree);
        setLocation($tree, $p2.start);
        }
      )*)?
    ;
    
multi_line_string returns[String text, Location location]
    : s=STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    | s=MULTI_LINE_STRING {
            $text = $s.text;
            $location = tokenLocation($s);
        }
    ;

param returns[AbstractDeclParam tree]
@init   {
    DeclParam tree;
        }
    : type ident {
        assert($ident.tree != null);
        $tree = new DeclParam($type.tree,  $ident.tree);
        setLocation($tree, $type.start);
        }
    ;
